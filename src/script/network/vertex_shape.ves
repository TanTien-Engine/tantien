import "network.pin" for Pin
import "maths.calc" for Calc
import "maths.vector" for Vector2
import "math" for Math

var SHAPE_CIRCLE    = 0
var SHAPE_CAPSULE_H = 1
var SHAPE_CAPSULE_V = 2
var SHAPE_ROD       = 3

var SHAPE_CIRCLE_RADIUS = 20

var SHAPE_CAPSULE_WIDTH  = 30
var SHAPE_CAPSULE_LENGTH = SHAPE_CAPSULE_WIDTH * 3

var PIN_RADIUS = 5

var PIN_CIRCLE_AROUND_IDX = 0

var PIN_CAPSULE_TOP_IDX    = 0
var PIN_CAPSULE_BOTTOM_IDX = 1
var PIN_CAPSULE_LEFT_IDX   = 2
var PIN_CAPSULE_RIGHT_IDX  = 3

var PIN_CIRCLE_NAMES = [ "around" ]
var PIN_CAPSULE_NAMES = [ "top", "bottom", "left", "right" ]

class VertexShape
{
	static calc_pin_pos(pin)
	{
		var e = pin.edge
		if (!e or !e.pin0 or !e.pin1) {
			return nil
		}

		var p0 = e.pin0.vert.pos
		var p1 = e.pin1.vert.pos
		if (pin == e.pin1) {
			p0 = e.pin1.vert.pos
			p1 = e.pin0.vert.pos
		}

		var pos = Vector2()

		if (pin.vert.shape == SHAPE_CIRCLE)
		{
			var angle = Math.atan2(p1.y - p0.y, p1.x - p0.x)

			var r = SHAPE_CIRCLE_RADIUS + PIN_RADIUS
			pos.x = p0.x + r * Math.cos(angle)
			pos.y = p0.y + r * Math.sin(angle)
		}
		else if (pin.vert.shape == SHAPE_CAPSULE_H)
		{
			if (pin.type == PIN_CAPSULE_TOP_IDX or pin.type == PIN_CAPSULE_BOTTOM_IDX)
			{
				var dx = 0
				var hw = SHAPE_CAPSULE_LENGTH * 0.5 - SHAPE_CAPSULE_WIDTH * 0.5			
				if (e.pin0.vert.shape == SHAPE_CAPSULE_H and e.pin1.vert.shape == SHAPE_CAPSULE_H)
				{
					var angle = Math.atan2(p1.y - p0.y, p1.x - p0.x)
					dx = Math.cos(angle) * SHAPE_CAPSULE_WIDTH
					dx = Math.max(Math.min(dx, hw), -hw)
				}
				else
				{
					dx = Math.max(Math.min(p1.x - p0.x, hw), -hw)
				}
				pos.x = p0.x + dx

				if (pin.type == PIN_CAPSULE_TOP_IDX) {
					pos.y = p0.y + SHAPE_CAPSULE_WIDTH * 0.5 + PIN_RADIUS
				} else if (pin.type == PIN_CAPSULE_BOTTOM_IDX) {
					pos.y = p0.y - SHAPE_CAPSULE_WIDTH * 0.5 - PIN_RADIUS
				}
			}
			else
			{
				pos.y = p0.y			
				if (pin.type == PIN_CAPSULE_LEFT_IDX) {
					pos.x = p0.x - SHAPE_CAPSULE_LENGTH * 0.5 - PIN_RADIUS
				} else if (pin.type == PIN_CAPSULE_RIGHT_IDX) {
					pos.x = p0.x + SHAPE_CAPSULE_LENGTH * 0.5 + PIN_RADIUS
				}
			}
		}
		else if (pin.vert.shape == SHAPE_CAPSULE_V)
		{
			if (pin.type == PIN_CAPSULE_TOP_IDX or pin.type == PIN_CAPSULE_BOTTOM_IDX)
			{
				pos.x = p0.x
				if (pin.type == PIN_CAPSULE_TOP_IDX) {
					pos.y = p0.y + SHAPE_CAPSULE_LENGTH * 0.5 + PIN_RADIUS
				} else if (pin.type == PIN_CAPSULE_BOTTOM_IDX) {
					pos.y = p0.y - SHAPE_CAPSULE_LENGTH * 0.5 - PIN_RADIUS
				} 
			}
			else
			{
				var hh = SHAPE_CAPSULE_LENGTH * 0.5 - SHAPE_CAPSULE_WIDTH * 0.5
				var dy = Math.max(Math.min(p1.y - p0.y, hh), -hh)
				pos.y = p0.y + dy

				if (pin.type == PIN_CAPSULE_LEFT_IDX) {
					pos.x = p0.x - SHAPE_CAPSULE_WIDTH * 0.5 - PIN_RADIUS
				} else if (pin.type == PIN_CAPSULE_RIGHT_IDX) {
					pos.x = p0.x + SHAPE_CAPSULE_WIDTH * 0.5 + PIN_RADIUS
				}
			}			
		}		

		return pos
	}

	static query_pin(vert, pos)
	{
		var sz = this.calc_aabb(vert.shape)
		var hw = sz.x * 0.5
		var hh = sz.y * 0.5
		if (pos.x < vert.pos.x - hw or
		    pos.x > vert.pos.x + hw or
		    pos.y < vert.pos.y - hh or
		    pos.y > vert.pos.y + hh) {
		    return nil
		}

		for (var pin in vert.pins)
		{
			var pin_pos = this.calc_pin_pos(pin)
			var dist = Calc.dis_pos_to_pos(pos, pin_pos)
			if (dist < PIN_RADIUS * 1.1) {
				return pin
			}
		}

		if (vert.shape == SHAPE_CIRCLE)
		{
			var dist = Calc.dis_pos_to_pos(pos, vert.pos)
			if (dist < SHAPE_CIRCLE_RADIUS + PIN_RADIUS * 2 and dist > SHAPE_CIRCLE_RADIUS) {
				return Pin(vert, PIN_CIRCLE_AROUND_IDX)
			}
		}
		else if (vert.shape == SHAPE_CAPSULE_H)
		{
			var hw = SHAPE_CAPSULE_LENGTH * 0.5 - SHAPE_CAPSULE_WIDTH * 0.5
			var hh = SHAPE_CAPSULE_WIDTH * 0.5
			if (pos.y > vert.pos.y + hh) {
				return Pin(vert, PIN_CAPSULE_TOP_IDX)
			} else if (pos.y < vert.pos.y - hh) {
				return Pin(vert, PIN_CAPSULE_BOTTOM_IDX)
			} else if (pos.x < vert.pos.x - hw) {
				return Pin(vert, PIN_CAPSULE_LEFT_IDX)
			} else if (pos.x > vert.pos.x + hw) {
				return Pin(vert, PIN_CAPSULE_RIGHT_IDX)
			}
		}
		else if (vert.shape == SHAPE_CAPSULE_V)
		{
			var hw = SHAPE_CAPSULE_WIDTH * 0.5
			var hh = SHAPE_CAPSULE_LENGTH * 0.5 - SHAPE_CAPSULE_WIDTH * 0.5
			if (pos.y > vert.pos.y + hh) {
				return Pin(vert, PIN_CAPSULE_TOP_IDX)
			} else if (pos.y < vert.pos.y - hh) {
				return Pin(vert, PIN_CAPSULE_BOTTOM_IDX)
			} else if (pos.x < vert.pos.x - hw) {
				return Pin(vert, PIN_CAPSULE_LEFT_IDX)
			} else if (pos.x > vert.pos.x + hw) {
				return Pin(vert, PIN_CAPSULE_RIGHT_IDX)
			}
		}

		return nil
	}

	static calc_aabb(shape)
	{
		var size = nil
		if (shape == SHAPE_CIRCLE)
		{
			var diam = SHAPE_CIRCLE_RADIUS * 2
			size = Vector2(diam, diam)
		}
		else if (shape == SHAPE_CAPSULE_H)
		{
			size = Vector2(SHAPE_CAPSULE_LENGTH, SHAPE_CAPSULE_WIDTH)
		}
		else if (shape == SHAPE_CAPSULE_V)
		{
			size = Vector2(SHAPE_CAPSULE_WIDTH, SHAPE_CAPSULE_LENGTH)
		}

		if (!size) {
			return nil
		}

		size.x = size.x + PIN_RADIUS * 4
		size.y = size.y + PIN_RADIUS * 4

		return size
	}

	static get_match_pin_idx(f_pin, t_node)
	{
		var expects = f_pin.vert.pin_types_t[f_pin.type]
		for (var i in 0 .. expects.count) {
			for (var j in 0 .. t_node.pin_types_f.count) {
				if (expects[i] == t_node.pin_types_f[j]) {
					return j
				}
			}
		}

		return -1
	}

	static get_vert_pin_names(vert)
	{
		if (vert.shape == SHAPE_CIRCLE) {
			return PIN_CIRCLE_NAMES
		} else if (vert.shape == SHAPE_CAPSULE_H or vert.shape == SHAPE_CAPSULE_V) {
			return PIN_CAPSULE_NAMES
		} else {
			return nil
		}
	}

	static pin_type_to_name(vert, type)
	{
		var names = this.get_vert_pin_names(vert)
		if (!names) {
			return nil
		} else {
			return names[type]
		}
	}

	static pin_name_to_type(vert, name)
	{
		var names = this.get_vert_pin_names(vert)
		if (!names) {
			return nil
		} else {
			for (var i in 0 .. names.count) {
				if (names[i] == name) {
					return i
				}
			}
			return -1
		}
	}
}