import "maths.vector" for Vector2
import "graphics" for Graphics

class Camera2D
{
	init() 
	{
		this.pos = Vector2()
		this.scale = 1.0

		this.size = Vector2()
	}

	on_size(w, h)
	{
		this.size.set(w, h)
	}

	screen2world(screen) 
	{
		return Camera2D.screen2world(screen, this.size.x, this.size.y, this.scale, this.scale, this.pos.x, this.pos.y)
	}

	world2screen(project)
	{
		var screen = Vector2()
		screen.x = (project.x - this.pos.x) / this.scale + this.size.x * 0.5
		screen.y = this.size.y * 0.5 - (project.y - this.pos.y) / this.scale
		return screen
	}

	translate_cam(offset)
	{
		this.pos.x = this.pos.x + offset.x * this.scale
		this.pos.y = this.pos.y + offset.y * this.scale

		Graphics.on_cam_update(this.pos.x, this.pos.y, this.scale)	
	}

	scale_cam(scale, pos)
	{
		var x = pos.x
		var y = this.size.y - pos.y
		var hw = this.size.x * 0.5
		var hh = this.size.y * 0.5
		var new_scale = this.scale * scale
		this.pos.x = (x - hw) * this.scale + this.pos.x - (x - hw) * new_scale
		this.pos.y = (y - hh) * this.scale + this.pos.y - (y - hh) * new_scale
		this.scale = new_scale

		Graphics.on_cam_update(this.pos.x, this.pos.y, this.scale)
	}

	reset()
	{
		this.pos.set(0, 0)
		this.scale = 1.0

		Graphics.on_cam_update(this.pos.x, this.pos.y, this.scale)	
	}

	static screen2world(screen, screen_width, screen_height, scale_x, scale_y, pos_x, pos_y)
	{
		var proj = Vector2()
		proj.x = (screen.x - screen_width * 0.5)  * scale_x + pos_x
		proj.y = (screen_height * 0.5 - screen.y) * scale_y + pos_y
		return proj
	}
}