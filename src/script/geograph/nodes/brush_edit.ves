import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for VAR_TYPE_NUMBER3
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geograph.brush" for Brush, BrushParts
import "maths.calc" for Calc
import "maths.compiler" for Compiler as maths_Compiler
import "maths" for Float3

import "maths.vector" for Vector3

class BrushEdit is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "parts",     VAR_TYPE_GEO),
			Pin(this, "translate", VAR_TYPE_NUMBER3),
			Pin(this, "rotate",    VAR_TYPE_NUMBER3),
			Pin(this, "scale",     VAR_TYPE_NUMBER3),
		]
		this.exports = [
			Pin(this, "brush", VAR_TYPE_GEO),
			Pin(this, "parts", VAR_TYPE_GEO),			
		]

		this.layout()
	}

	calc_value(idx)
	{
		var v_parts = Blueprint.calc_input_value(this, 0)
		if (!v_parts or v_parts.type != VAR_TYPE_GEO or !(v_parts.value.shape is BrushParts)) {
			return nil
		}

		var mat = this.calc_mat_value()

		var parts = v_parts.value.shape.clone()
		var brush = parts.brush
 
		// points
		for (var pair in parts.points)
		{
			var poly = brush.polys[pair[0]]
			var point = poly.get_points()[pair[1]]

			var pos = point.get_pos()
			var p = Float3.init(pos[0], pos[1], pos[2])
			p.transform(mat)
			point.set_pos([ p.x(), p.y(), p.z() ])

			poly.set_topo_dirty()
		}

		// faces
		for (var pair in parts.faces)
		{
			var poly = parts.brush.polys[pair[0]]
			var face = poly.get_faces()[pair[1]]

			var border = face.get_border()
			var pts = poly.get_points()
			for (var i = 0; i < border.count; i = i + 1)
			{
				var point = pts[border[i]]

				var pos = point.get_pos()
				var p = Float3.init(pos[0], pos[1], pos[2])
				p.transform(mat)

				point.set_pos([ p.x(), p.y(), p.z() ])
			}

			poly.set_topo_dirty()
		}

		brush.set_model_dirty()

		if (idx == 0)
		{
			var new_geo = VarGeo()
			new_geo.shape = brush
			return Variant(VAR_TYPE_GEO, new_geo)
		}
		else if (idx == 1)
		{
			var new_geo = VarGeo()
			new_geo.shape = parts
			return Variant(VAR_TYPE_GEO, new_geo)
		}

		return nil
	}

	calc_opcodes(compiler, opcodes, idx)
	{
		if (idx == 0)
		{
			var r_mat = this.calc_mat_opcodes(compiler, opcodes)
			if (!r_mat) {
				return nil
			}

			var r_part = Blueprint.calc_input_opcodes(this, 0, compiler, opcodes)

			var r_poly = compiler.new_reg()
			opcodes.vector_get(r_poly, r_part, 0)

			var r_pts = compiler.new_reg()
			opcodes.vector_get(r_pts, r_part, 1)

			opcodes.transform(r_pts, r_mat)
			opcodes.polytope_set_dirty(r_poly)

			compiler.free_reg(r_pts)
			compiler.free_reg(r_mat)
			compiler.free_reg(r_part)

			return r_poly
		}
		else
		{
			import "logger.logger" for Logger
			Logger.error("Not Implemented!")
						
			return nil
		}
	}

	calc_mat_value()
	{
		var v_translate = Blueprint.calc_input_value(this, 1)
		var v_rotate = Blueprint.calc_input_value(this, 2)
		var v_scale = Blueprint.calc_input_value(this, 3)
		return Calc.transform_mat(v_scale, v_rotate, v_translate)
	}

	calc_mat_opcodes(compiler, opcodes)
	{
		var r_mat = maths_Compiler.create_mat4(compiler, opcodes)

		// scale
		var r_scale = Blueprint.calc_input_opcodes(this, 3, compiler, opcodes)
		if (r_scale) 
		{
			opcodes.mat4_scale(r_mat, r_scale)
			compiler.free_reg(r_scale)
		}

		// rotate
		var r_rotate = Blueprint.calc_input_opcodes(this, 2, compiler, opcodes)		
		if (r_rotate) 
		{
			opcodes.mat4_rotate(r_mat, r_rotate)
			compiler.free_reg(r_rotate)
		}

		// translate
		var r_translate = Blueprint.calc_input_opcodes(this, 1, compiler, opcodes)
		if (r_translate) 
		{
			opcodes.mat4_translate(r_mat, r_translate)
			compiler.free_reg(r_translate)
		}

		return r_mat
	}
}