import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_NUMBER3, VAR_TYPE_ARRAY
import "blueprint.node_param" for NodeParam
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VAR_TYPE_PLANE, VarGeo
import "geograph.brush" for Brush
import "geograph.compiler" for Compiler as geo_Compiler
import "geograph.poly_builder" for PolyBuilder
import "maths.vector" for Vector3
import "maths.compiler" for Compiler as math_Compiler
import "maths" for Plane as tt_Plane, Maths, Matrix44, Float3

class Plane is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "origin",    VAR_TYPE_NUMBER3),
			Pin(this, "direction", VAR_TYPE_NUMBER3),
			Pin(this, "3pos",      VAR_TYPE_ARRAY),			
		]
		this.exports = [
			Pin(this, "geo",   VAR_TYPE_GEO),
			Pin(this, "plane", VAR_TYPE_PLANE),
		]

		this.params.add(NodeParam("ori", Vector3(0, 0, 0)))
		this.params.add(NodeParam("dir", Vector3(0, 1, 0), { "min" : -1, "max" : 1 }))

		this.layout()
	}

	calc_value(idx)
	{
		if (idx == 0) {
			return this.calc_plane_geo()
		} else if (idx == 1) {
			return this.calc_plane_prim()
		}
		return nil
	}

	calc_opcodes(compiler, opcodes, idx)
	{
		if (idx == 1)
		{
			var r_3pos = Blueprint.calc_input_opcodes(this, 2, compiler, opcodes)
			if (r_3pos < 0)
			{
				var r_ori = Blueprint.calc_input_opcodes(this, 0, compiler, opcodes)
				if (r_ori < 0) {
					r_ori = math_Compiler.create_vec3(compiler, opcodes, this.query_param("ori").value)
				}

				var r_dir = Blueprint.calc_input_opcodes(this, 1, compiler, opcodes)
				if (r_dir < 0) {
					r_dir = math_Compiler.create_vec3(compiler, opcodes, this.query_param("dir").value)
				}

				var r_dst = compiler.new_reg()
				if (r_dst < 0) {
					return -1
				}

				opcodes.create_plane_2(r_dst, r_ori, r_dir)

				return r_dst
			}
			else
			{
				var p0 = v_3pos.value[0].value
				var p1 = v_3pos.value[1].value
				var p2 = v_3pos.value[2].value

				var r_dst = geo_Compiler.create_plane_i(compiler, opcodes, p0, p1, p2)

				return r_dst
			}
		}
		else
		{
			import "logger.logger" for Logger
			Logger.error("Not Implemented!")

			return -1
		}
	}

	calc_plane_geo()
	{
		var ori = this.calc_param_ori()
		var dir = this.calc_param_dir()

		var size = 1.0
		var quad = [
			Float3.init(-size, 0, -size),
			Float3.init(-size, 0,  size),
			Float3.init( size, 0,  size),
			Float3.init( size, 0, -size),
		]

		var rot_mat = Maths.calc_rot_mat([ 0, 1, 0 ], [ dir.x, dir.y, dir.z ])
		for (var p in quad) {
			p.transform(rot_mat)
		}

		var points = []
		for (var p in quad) 
		{
			var x = ori.x + p.x()
			var y = ori.y + p.y()
			var z = ori.z + p.z()
			points.add(Vector3(x, y, z))
		}

		var faces = []
		faces.add([ 0, 1, 2, 3 ])

		var poly = PolyBuilder.build_poly(points, faces)

		var geo = VarGeo()
		geo.shape = Brush([ poly ])
		return Variant(VAR_TYPE_GEO, geo)
	}

	calc_plane_prim()
	{
		var v_3pos = Blueprint.calc_input_value(this, 2)
		if (v_3pos)
		{
			var pos3 = []
			for (var i = 0; i < 3; i = i + 1) 
			{
				var src = v_3pos.value[i].value
				var dst = []
				dst.add(src.x)
				dst.add(src.y)
				dst.add(src.z)
				pos3.add(dst)
			}

			var plane = tt_Plane.init(pos3[0], pos3[1], pos3[2])
			return Variant(VAR_TYPE_PLANE, plane)
		}
		else
		{
			var ori = this.calc_param_ori()
			var dir = this.calc_param_dir()
			var plane = tt_Plane.init([ ori.x, ori.y, ori.z ], [ dir.x, dir.y, dir.z ])
			return Variant(VAR_TYPE_PLANE, plane)
		}		
	}

	calc_param_ori()
	{
		var ori = nil
		var v_ori = Blueprint.calc_input_value(this, 0)
		if (v_ori) {
			ori = v_ori.value
		} else {
			ori = this.query_param("ori").value
		}
		return ori
	}

	calc_param_dir()
	{
		var dir = nil
		var v_dir = Blueprint.calc_input_value(this, 1)
		if (v_dir) {
			dir = v_dir.value
		} else {
			dir = this.query_param("dir").value
		}
		return dir
	}
}