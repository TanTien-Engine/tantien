import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.nodes.array" for Array
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY, VAR_TYPE_NUMBER2
import "blueprint.node_param" for NodeParam
import "blueprint.node_editor" for NodeEditor
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geograph.config" for POINT_RADIUS
import "geograph.brush" for Brush
import "maths.vector" for Vector3
import "geometry" for Polygon as tt_Polygon, Circle, PolyPoint, PolyFace, Polytope

var EDITOR = nil

class Polygon is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "vertices", VAR_TYPE_ARRAY),
		]
		this.exports = [
			Pin(this, "geo",      VAR_TYPE_GEO),
			Pin(this, "polytope", VAR_TYPE_GEO),
		]

		this.params.add(NodeParam("vertices", []))

		// draw
		this.params.add(NodeParam("color", Vector3(1.0, 0.0, 0.0)))

		this.layout()

		this.editor_polyline = Array()
	}

	on_pin_dirty(pin)
	{
		if (pin.is_input) {
			for (var pin in this.exports) {
				Blueprint.send_pin_dirty(pin)
			}
		}
	}
	
	calc_value(idx)
	{
		if (idx == 0)
		{
			var geo = VarGeo()
			geo.shape = tt_Polygon.init()
			this.update_geo(geo)
			return Variant(VAR_TYPE_GEO, geo)
		}
		else if (idx == 1)
		{
			var vertices = this.calc_vertices()
			if (!vertices or vertices.count < 3 * 2) {
				return nil
			}

			var points = []
			var border = []
			var n = vertices.count / 2
			for (var i = 0; i < n; i = i + 1)
			{
				var x = vertices[i * 2]
				var y = vertices[i * 2 + 1]
				points.add(PolyPoint.init([ x, 0, y ]))

				border.add(i)
			}

			var face = PolyFace.init(border, [])
			var poly = Polytope.init(points, [ face ])

			var geo = VarGeo()
			geo.shape = Brush([ poly ])
			return Variant(VAR_TYPE_GEO, geo)
		}
		return nil
	}

	geo_solver_gen(scene)
	{
		var geo = VarGeo()
		geo.shape = tt_Polygon.init()
		this.update_geo(geo)
		geo.geo_id = scene.add_geo(geo.shape)
	}

	calc_vertices()
	{
		var v_verts = Blueprint.calc_input_value(this, 0)
		if (v_verts)
		{
			if (v_verts.type != VAR_TYPE_ARRAY or v_verts.value.isEmpty) {
				return nil
			}

			var vertices = []

			if (v_verts.value[0] is Num)
			{
				for (var v in v_verts.value) {
					vertices.add(v)
				}
			}
			else
			{
				var type = v_verts.value[0].type

				if (type == VAR_TYPE_NUMBER2)
				{
					for (var vert in v_verts.value) {
						vertices.add(vert.value.x)
						vertices.add(vert.value.y)
					}
				}
				else if (type == VAR_TYPE_GEO)
				{
					for (var shape in v_verts.value) {
						var circle = shape.value.shape.get()
						vertices.add(circle[0])
						vertices.add(circle[1])
					}
				}
			}

			return vertices
		}
		else
		{
			return this.query_param("vertices").value
		}
	}

	update_geo(geo)
	{
		var vertices = this.calc_vertices()
		if (vertices) {
			geo.shape.set_vertices(vertices)
		}

		geo.render_style["color"] = this.query_param("color").value.clone()
	}

	bind_editor()
	{
		if (!EDITOR) {
			EDITOR = NodeEditor("assets/blueprints/editorgraph/polyline_edit.ves")
		}

		this.editor_polyline.list.clear()
		for (var i = 0; i < this.params[0].value.count; i = i + 2)
		{
			var x = this.params[0].value[i]
			var y = this.params[0].value[i + 1]

			var geo = VarGeo()
			geo.shape = Circle.init(x, y, POINT_RADIUS)
			this.editor_polyline.list.add(Variant(VAR_TYPE_GEO, geo))
		}

		Blueprint.connect(this.editor_polyline.exports[0], EDITOR.editor_node.imports[1])

		Blueprint.send_node_dirty_root(EDITOR.editor_node)

		return true
	}

	draw_editor() 
	{
		if (EDITOR) {
			EDITOR.draw()		
		}
	}

	on_mouse_pressed(x, y, btn) 
	{
		EDITOR.on_mouse_pressed(x, y, btn)

		this.editor_flush()
	}

	on_mouse_released(x, y, btn) 
	{
		EDITOR.on_mouse_released(x, y, btn)

		this.editor_flush()
	}

	on_mouse_dragged(x, y, btn) 
	{
		EDITOR.on_mouse_dragged(x, y, btn)

		this.editor_flush()
	}

	on_mouse_moved(x, y, btn) 
	{
		EDITOR.on_mouse_moved(x, y, btn)
	}

	editor_flush()
	{
		var circles = this.editor_polyline.list

		this.params[0].value.clear()
		for (var circle in circles) 
		{
			var c = circle.value.shape.get()
			this.params[0].value.add(c[0])
			this.params[0].value.add(c[1])
		}

		Blueprint.send_pin_dirty_root(this.exports[0])
	}
}