import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY
import "blueprint.node_param" for NodeParam
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geograph.brush" for Brush
import "geometry" for Polytope
import "vm" for CodeTools, CodeStats

class Boolean is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "a", [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
			Pin(this, "b", [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
		]
		this.exports = [
			Pin(this, "geo", [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
		]

		this.params.add(NodeParam("operator", "intersect", { "combo" : [ "union", "intersect", "subtract" ] }))

		this.layout()
	}
	
	calc_value(idx)
	{
		var v_a = Blueprint.calc_input_value(this, 0)
		if (!v_a) {
			return nil
		}
		var v_b = Blueprint.calc_input_value(this, 1)
		if (!v_b) {
			return nil
		}

		var b = []
		this.get_polys(b, v_b)

		var operator = this.query_param("operator").value

		var brushes = []
		this.boolean_impl(brushes, v_a, b, operator)

		if (brushes.isEmpty) {
			return nil
		} else if (brushes.count == 1) {
			return brushes[0]
		} else {
			return Variant(brushes)
		}
	}

	calc_opcodes(compiler, opcodes, idx)
	{
		var ori_size = CodeTools.get_size(opcodes)
		var ori_cost = CodeStats.get_cost(opcodes)

		var r_base = Blueprint.calc_input_opcodes(this, 0, compiler, opcodes)
		if (r_base < 0) {
			return -1
		}

		var r_tool = Blueprint.calc_input_opcodes(this, 1, compiler, opcodes)
		if (r_tool < 0) {
			return -1
		}

		var r_dst = compiler.new_reg()

		opcodes.polytope_subtract(r_dst, r_base, r_tool)

		CodeStats.add_cost(opcodes, 1)

		compiler.free_reg(r_base)
		compiler.free_reg(r_tool)

		var curr_size = CodeTools.get_size(opcodes)
		var hash = CodeTools.hash(opcodes, ori_size, curr_size)
		CodeStats.add_code_block(opcodes, ori_size, curr_size, r_dst)

		return r_dst
	}

	get_polys(dst, src)
	{
		if (src.type == VAR_TYPE_ARRAY) 
		{
			for (var v in src.value) {
				this.get_polys(dst, v)
			}
		} 
		else if (src.type == VAR_TYPE_GEO and src.value.shape is Brush) 
		{
			var brush = src.value.shape
			for (var p in src.value.shape.polys) {
				dst.add(p)
			}
		}
	}

	boolean_impl(dst, v_a, b, op)
	{
		if (v_a.type == VAR_TYPE_ARRAY)
		{
			var list = []

			for (var item in v_a.value) {
				this.boolean_impl(list, item, b, op)
			}

			if (!list.isEmpty) 
			{
				if (list.count == 1) {
					dst.add(list[0])
				} else {
					dst.add(Variant(list))
				}
			}
		}
		else if (v_a.type == VAR_TYPE_GEO and v_a.value.shape is Brush)
		{
			var brush = v_a.value.shape
			if (brush.lock)
			{
				var geo = VarGeo()
				geo.shape = brush.clone()
				dst.add(Variant(VAR_TYPE_GEO, geo))
			}
			else
			{
				var d = this.boolean_impl(brush.polys, b, op)
				if (d) {
					dst.add(d)
				}
			}
		}
	}

	boolean_impl(a, b, op)
	{
		var polys = Polytope.boolean(op, a, b)
		if (polys.isEmpty) {
			return nil
		} else {
			var geo = VarGeo()
			geo.shape = Brush(polys)
			return Variant(VAR_TYPE_GEO, geo)
		}
	}
}