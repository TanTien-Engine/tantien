import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "geograph.variant" for VAR_TYPE_GEO, VAR_TYPE_CUBE, VarGeo
import "geograph.brush" for BrushParts
import "maths.cube" for Cube
import "maths.vector" for Vector3

class SelPolyPoints is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "brush", VAR_TYPE_GEO),
			Pin(this, "cube",  VAR_TYPE_CUBE),
		]
		this.exports = [
			Pin(this, "parts", VAR_TYPE_GEO),
		]

		this.layout()
	}
	
	calc_value(idx)
	{
		var v_brush = Blueprint.calc_input_value(this, 0)
		if (!v_brush) {
			return nil
		}

		var v_cube = Blueprint.calc_input_value(this, 1)
		if (!v_cube) {
			return nil
		}

		var parts = BrushParts()

		parts.brush = v_brush.value.shape

		var n_poly = v_brush.value.shape.polys.count
		for (var i_poly = 0; i_poly < n_poly; i_poly = i_poly + 1)
		{
			var poly = v_brush.value.shape.polys[i_poly]
			var points = poly.get_points()
			for (var i_pt = 0; i_pt < points.count; i_pt = i_pt + 1) 
			{
				var pos = points[i_pt].get_pos()
				if (v_cube.value.is_contain_pos(Vector3(pos[0], pos[1], pos[2]))) {
					parts.points.add([ i_poly, i_pt ])
				}
			}
		}

		var geo = VarGeo()
		geo.shape = parts
		return Variant(VAR_TYPE_GEO, geo)
	}

	calc_opcodes(compiler, opcodes, idx)
	{
		var r_poly = Blueprint.calc_input_opcodes(this, 0, compiler, opcodes)
		var r_cube = Blueprint.calc_input_opcodes(this, 1, compiler, opcodes)

		var r_dst = compiler.new_reg()
		opcodes.create_vector(r_dst)

		var r_pts = compiler.new_reg()

		opcodes.polypoint_select(r_pts, r_poly, r_cube)

		opcodes.vector_add(r_dst, r_poly)
		opcodes.vector_add(r_dst, r_pts)

		compiler.free_reg(r_poly)
		compiler.free_reg(r_cube)
		compiler.free_reg(r_pts)

		return r_dst
	}
}