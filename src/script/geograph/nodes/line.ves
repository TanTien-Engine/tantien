import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.nodes.number2" for Number2
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY, VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2
import "blueprint.node_param" for NodeParam
import "blueprint.node_editor" for NodeEditor
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geometry" for Line as tt_Line
import "math" for Math
import "maths.vector" for Vector2

var EDITOR = nil

class Line is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "p0", [ VAR_TYPE_NUMBER2, VAR_TYPE_ARRAY ]),
			Pin(this, "p1", [ VAR_TYPE_NUMBER2, VAR_TYPE_ARRAY ]),
		]
		this.exports = [
			Pin(this, "geo",   [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
			Pin(this, "angle", VAR_TYPE_NUMBER),
		]

		this.params.add(NodeParam("x0", 0.0, { "min" : -1000, "max" : 1000 }))
		this.params.add(NodeParam("y0", 0.0, { "min" : -1000, "max" : 1000 }))
		this.params.add(NodeParam("x1", 100.0, { "min" : -1000, "max" : 1000 }))
		this.params.add(NodeParam("y1", 100.0, { "min" : -1000, "max" : 1000 }))

		this.layout()

		this.editor_p0 = Number2()
		this.editor_p1 = Number2()		
	}
	
	calc_value(idx)
	{
		if (idx == 0)
		{
			var v_p0 = Blueprint.calc_input_value(this, 0)
			var v_p1 = Blueprint.calc_input_value(this, 1)

			if ((v_p0 and v_p0.type == VAR_TYPE_ARRAY) or (v_p1 and v_p1.type == VAR_TYPE_ARRAY))
			{
				var lines = this.gen_multi_lines(v_p0, v_p1)
				if (lines.isEmpty) {
					return nil
				} else {
					return Variant(lines)				
				}
			}
			else
			{
				var geo = VarGeo()
				geo.shape = tt_Line.init()
				this.update_geo(geo)
				return Variant(VAR_TYPE_GEO, geo)
			}
		}
		else if (idx == 1)
		{
			var v_p0 = Blueprint.calc_input_value(this, 0)
			var v_p1 = Blueprint.calc_input_value(this, 1)
			if (!v_p0 or !v_p1) {
				return nil
			}

			var s = v_p0.value
			var e = v_p1.value
			return Variant(Math.atan2(e.y - s.y, e.x - s.x))
		}

		return nil
	}

	update_geo(geo)
	{
		var x0 = 0
		var y0 = 0
		var x1 = 0
		var y1 = 0

		var v_p0 = Blueprint.calc_input_value(this, 0)
		if (v_p0 and (v_p0.type == VAR_TYPE_NUMBER2 or v_p0.type == VAR_TYPE_GEO)) {
			if (v_p0.type == VAR_TYPE_NUMBER2) {
				x0 = v_p0.value.x
				y0 = v_p0.value.y
			} else {
				var circle = v_p0.value.shape.get()
				x0 = circle[0]
				y0 = circle[1]
			}
		} else {
			x0 = this.query_param("x0").value
			y0 = this.query_param("y0").value
		}

		var v_p1 = Blueprint.calc_input_value(this, 1)
		if (v_p1 and (v_p1.type == VAR_TYPE_NUMBER2 or v_p1.type == VAR_TYPE_GEO)) {
			if (v_p1.type == VAR_TYPE_NUMBER2) {
				x1 = v_p1.value.x
				y1 = v_p1.value.y
			} else {
				var circle = v_p1.value.shape.get()
				x1 = circle[0]
				y1 = circle[1]
			}
		} else {
			x1 = this.query_param("x1").value
			y1 = this.query_param("y1").value
		}

		geo.shape.set(x0, y0, x1, y1)
	}

	get_points(v, list)
	{
		if (v is Vector2)
		{
			list.add(v)
		}
		else if (v.type == VAR_TYPE_ARRAY)
		{
			for (var c in v.value) {
				this.get_points(c, list)
			}
		}
		else if (v.type == VAR_TYPE_NUMBER2)
		{
			list.add(v.value)
		}
		else if (v.type == VAR_TYPE_GEO)
		{
			var c = v.value.shape.get()
			list.add(Vector2(c[0], c[1]))
		}
	}	

	gen_multi_lines(v_p0, v_p1)
	{
		var p0s = []
		var p1s = []

		this.get_points(v_p0, p0s)
		this.get_points(v_p1, p1s)

		if (p0s.isEmpty or p1s.isEmpty) {
			return []
		}

		var ret = []

		for (var i = 0; i < p0s.count or i < p1s.count; i = i + 1)
		{
			var p0
			if (i < p0s.count) {
				p0 = p0s[i]
			} else {
				p0 = p0s.back()
			}

			var p1
			if (i < p1s.count) {
				p1 = p1s[i]
			} else {
				p1 = p1s.back()
			}

			var geo = VarGeo()
			geo.shape = tt_Line.init()
			geo.shape.set(p0.x, p0.y, p1.x, p1.y)
			ret.add(Variant(VAR_TYPE_GEO, geo))
		}

		return ret
	}

	bind_editor()
	{
		if (!EDITOR) {
			EDITOR = NodeEditor("assets/blueprints/editorgraph/line_edit.ves")
		}

		var x0 = this.query_param("x0").value
		var y0 = this.query_param("y0").value
		var x1 = this.query_param("x1").value
		var y1 = this.query_param("y1").value
		this.editor_p0.value.set(x0, y0)
		this.editor_p1.value.set(x1, y1)

		Blueprint.connect(this.editor_p0.exports[0], EDITOR.editor_node.imports[1])
		Blueprint.connect(this.editor_p1.exports[0], EDITOR.editor_node.imports[2])

		Blueprint.send_node_dirty_root(EDITOR.editor_node)

		return true
	}

	draw_editor() 
	{
		if (EDITOR) {
			EDITOR.draw()		
		}
	}

	on_mouse_pressed(x, y, btn) 
	{
		EDITOR.on_mouse_pressed(x, y, btn)
	}

	on_mouse_released(x, y, btn) 
	{
		EDITOR.on_mouse_released(x, y, btn)
	}

	on_mouse_dragged(x, y, btn) 
	{
		EDITOR.on_mouse_dragged(x, y, btn)

		this.params[0].value = this.editor_p0.value.x
		this.params[1].value = this.editor_p0.value.y
		this.params[2].value = this.editor_p1.value.x
		this.params[3].value = this.editor_p1.value.y

		Blueprint.send_pin_dirty_root(this.exports[0])
	}
}