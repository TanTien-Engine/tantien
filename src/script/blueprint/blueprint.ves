import "blueprint.connecting" for Connecting
import "blueprint.variant" for VAR_TYPE_PORT, VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2, VAR_TYPE_NUMBER3, VAR_TYPE_NUMBER4
import "blueprint.dirty_types" for DIRTY_CONNECTION
import "geograph.variant" for VAR_TYPE_GEO
import "vm" for CodeTools

var PIN_DIRTY_TIMESTAMP 	= 0
var NODE_DIRTY_TIMESTAMP 	= 0
var ENABLE_PASS_PIN_DIRTY 	= true

class Blueprint
{
	static topo_sort(nodes)
	{
		// prepare
		var in_deg = List.filled(nodes.count, 0)
		var out_nodes = []
		for (var i = 0; i < nodes.count; i = i + 1) {
			out_nodes.add([])
		}	
		for (var i = 0; i < nodes.count; i = i + 1)
		{
			for (var pin in nodes[i].imports)
			{
				if (!pin.stop_port_pass)
				{
					for (var conn in pin.conns)
					{
						if (!conn.from.stop_port_pass)
						{
							for (var j = 0; j < nodes.count; j = j + 1)
							{
								if (nodes[j] == conn.from.node) {
									in_deg[i] = in_deg[i] + 1
									out_nodes[j].add(i)
									// todo break
									// break;
								}
							}
						}
					}
				}
			}
		}

		// sort
		var st = []
		for (var i = 0; i < in_deg.count; i = i + 1)
		{
			if (in_deg[i] == 0) {
				st.add(i)
			}
		}
		var ret = []
		while (st.count > 0)
		{
			var v = st[st.count - 1]
			st.removeAt(st.count - 1)
			ret.add(nodes[v])
			for (var i in out_nodes[v])
			{
				in_deg[i] = in_deg[i] - 1
				if (in_deg[i] == 0) {
					st.add(i)
				}
			}
		}

		return ret
	}

	static get_precursor_nodes(root)
	{
		var nodes = Set.new()
		var buf = []
		buf.add(root)
		while (!buf.isEmpty)
		{
			var n = buf[buf.count - 1]
			buf.removeAt(buf.count - 1)

			nodes.add(n)
			for (var pin in n.imports) {
				if (!pin.stop_port_pass) {
					for (var conn in pin.conns) {
						if (!conn.from.stop_port_pass) {
							buf.add(conn.from.node)
						}
					}
				}
			}
		}
		return nodes
	}

	static get_successor_nodes(root)
	{
		var nodes = Set.new()
		var buf = []
		buf.add(root)
		while (!buf.isEmpty)
		{
			var n = buf[buf.count - 1]
			buf.removeAt(buf.count - 1)

			nodes.add(n)
			for (var pin in n.exports) {
				if (!pin.stop_port_pass) {
					for (var conn in pin.conns) {
						if (!conn.to.stop_port_pass) {
							buf.add(conn.to.node)
						}
					}
				}
			}
		}
		return nodes
	}

	static connect(from, to)
	{
		PIN_DIRTY_TIMESTAMP = PIN_DIRTY_TIMESTAMP + 1

		// input pin only has one conn
		if (!to.conns.isEmpty) 
		{
			var conn = to.conns.front()

			conn.from.conns.remove(conn)
			conn.to.conns.remove(conn)
			conn.update()

			conn.from.node.on_disconnected(conn.from)

			conn.from.node.on_pin_dirty(conn.from, DIRTY_CONNECTION, nil)
		}

		var conn = Connecting(from, to)
		from.conns.add(conn)
		to.conns.add(conn)
		conn.update()

		from.node.on_connected(from)
		to.node.on_connected(to)

		conn.from.node.on_pin_dirty(conn.from, DIRTY_CONNECTION, nil)
		conn.to.node.on_pin_dirty(conn.to, DIRTY_CONNECTION, nil)

		return conn
	}

	static query_pin(pins, slot)
	{
		if (slot is Num) {
			if (slot >= 0 and slot < pins.count) {
				return pins[slot]
			}
		} else if (slot is String) {
			for (var p in pins) {
				if (p.name == slot) {
					return p
				}
			}
		}
		return nil
	}

	static connect(f_node, f_pin, t_node, t_pin)
	{
		var f = this.query_pin(f_node.exports, f_pin)
		var t = this.query_pin(t_node.imports, t_pin)
		if (f and t) 
		{
			return this.connect(f, t)
		} 
		else 
		{
			import "logger.logger" for Logger
			Logger.error("connect fail %(f_pin) -> %(t_pin), %(f) %(t)")
			return nil
		}
	}

	static disconnect(conn)
	{
		conn.from.conns.remove(conn)
		conn.to.conns.remove(conn)
		conn.update()

		conn.from.node.on_disconnected(conn.from)
		conn.to.node.on_disconnected(conn.to)

		PIN_DIRTY_TIMESTAMP = PIN_DIRTY_TIMESTAMP + 1
		conn.from.node.on_pin_dirty(conn.from, DIRTY_CONNECTION, nil)
		conn.to.node.on_pin_dirty(conn.to, DIRTY_CONNECTION, nil)
	}

	static get_input_pin(curr_node, input_slot)
	{
		var pin = this.query_pin(curr_node.imports, input_slot)
		if (!pin or pin.conns.isEmpty) {
			return nil
		} else {
			return pin.conns.front().from
		}
	}

	static get_input_node(curr_node, input_slot)
	{
		var pin = this.get_input_pin(curr_node, input_slot)
		if (pin) {
			return pin.node
		}
		return nil
	}

	static get_output_pin(curr_node, output_slot)
	{
		var pin = this.query_pin(curr_node.exports, output_slot)
		if (!pin or pin.conns.isEmpty) {
			return nil
		} else {
			return pin.conns.front().to
		}
	}

	static get_output_node(curr_node, output_slot)
	{
		var pin = this.get_output_pin(curr_node, output_slot)
		if (pin) {
			return pin.node
		}
		return nil
	}

	static calc_input_value(curr_node, input_slot)
	{
		var pin = this.query_pin(curr_node.imports, input_slot)
		if (!pin or pin.conns.isEmpty) {
			return nil
		} else {
			var f_pin = pin.conns.front().from
			var f_node = f_pin.node
			if (f_node.has_method("calc_value(_)")) {
				f_node.calc_times = f_node.calc_times + 1
				return f_node.calc_value(f_pin.slot_idx)
			} else {
				return nil
			}
		}
	}

	static calc_output_value(node, idx)
	{
		if (!node.has_method("calc_value(_)")) {
			return nil
		}

		node.calc_times = node.calc_times + 1

		if (idx is String) 
		{
			for (var i in 0 .. node.exports.count) {
				if (node.exports[i].name == idx) {
					return node.calc_value(i)
				}
			}
			return nil
		}
		else
		{
			return node.calc_value(idx)
		}
	}

	static calc_input_opcodes(curr_node, input_slot, compiler, opcodes)
	{
		var pin = this.query_pin(curr_node.imports, input_slot)
		if (!pin or pin.conns.isEmpty) {
			return nil
		}

		var f_pin = pin.conns.front().from
		var f_node = f_pin.node

		if (f_node.has_method("calc_opcodes(_,_,_)")) 
		{
			var is_geo_pin = false
			if (f_pin.type is List)
			{
				for (var t in f_pin.type) {
					if (t == VAR_TYPE_GEO) {
						is_geo_pin = true
					}
				}
			}
			else
			{
				is_geo_pin = (f_pin.type == VAR_TYPE_GEO)
			}

			if (is_geo_pin) 
			{
				compiler.get_profiler().push_block(f_node.get_title(), CodeTools.get_size(opcodes))
			}
			var ret = f_node.calc_opcodes(compiler, opcodes, f_pin.slot_idx)
			if (is_geo_pin) 
			{
				var reg = -1
				if (ret) {
					reg = ret
				}
				compiler.get_profiler().pop_block(CodeTools.get_size(opcodes), reg)
			}
			return ret
		} 
		else 
		{
			import "logger.logger" for Logger
			Logger.error("No calc_opcodes in %(f_node) !")
		}

		return nil
	}

	static get_first_connect_pin(pin)
	{
		if (pin.conns.isEmpty) {
			return nil
		}
		if (pin.is_input) {
			return pin.conns.front().from
		} else {
			return pin.conns.front().to
		}
	}

	static is_out_conns_empty(node)
	{
		for (var p in node.exports) {
			if (!p.conns.isEmpty) {
				return false
			}
		}
		return true
	}

	static is_out_ports_empty(node)
	{
		var port_num = 0
		for (var p in node.exports) 
		{
			if (p.type == VAR_TYPE_PORT) {
				if (!p.conns.isEmpty) {
					return false
				} else {
					port_num = port_num + 1
				}
			}
		}

		return port_num > 0
	}

	static enable_pass_pin_dirty(enable)
	{
		ENABLE_PASS_PIN_DIRTY = enable
	}

	static send_pin_dirty(pin, type, msg)
	{
		if (!ENABLE_PASS_PIN_DIRTY or pin.dirty_timestamp == PIN_DIRTY_TIMESTAMP) {
			return
		}

		pin.dirty_timestamp = PIN_DIRTY_TIMESTAMP

		if (pin.is_input) 
		{
			for (var conn in pin.conns) 
			{
				var next = conn.from
				next.node.on_pin_dirty(next, type, msg)
			}
		} 
		else 
		{
			for (var conn in pin.conns) 
			{
				var next = conn.to
				next.node.on_pin_dirty(next, type, msg)
			}
		}
	}

	static send_pin_dirty_root(pin, type)
	{
		PIN_DIRTY_TIMESTAMP = PIN_DIRTY_TIMESTAMP + 1
		this.send_pin_dirty(pin, type, nil)
	}

	static send_pin_dirty_root(pin, type, msg)
	{
		PIN_DIRTY_TIMESTAMP = PIN_DIRTY_TIMESTAMP + 1
		this.send_pin_dirty(pin, type, msg)
	}

	static send_node_dirty(node)
	{
		if (node.dirty_timestamp == NODE_DIRTY_TIMESTAMP) {
			return
		}

		node.dirty_timestamp = NODE_DIRTY_TIMESTAMP

		node.on_node_dirty()

		for (var pin in node.exports) {
			for (var conn in pin.conns) {
				this.send_node_dirty(conn.to.node)
			}
		}
	}

	static send_node_dirty_root(node)
	{
		NODE_DIRTY_TIMESTAMP = NODE_DIRTY_TIMESTAMP + 1
		this.send_node_dirty(node)
	}
}