import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant, VAR_TYPE_ANY, VAR_TYPE_INTEGER, VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2, VAR_TYPE_NUMBER3, VAR_TYPE_NUMBER4
import "blueprint.nodes.add" for Add
import "maths.vector" for Vector2, Vector3, Vector4
import "math" for Math

class Divide is Node
{
	init() 
	{
		super.init()

		this.imports = [
			Pin(this, "a", VAR_TYPE_ANY),
			Pin(this, "b", VAR_TYPE_ANY)
		]
		this.exports = [
			Pin(this, "v", VAR_TYPE_ANY)
		]

		this.layout()
	}

	
	calc_value(idx)
	{
		var a = Blueprint.calc_input_value(this, 0)
		var b = Blueprint.calc_input_value(this, 1)
		if (!a and !b) {
			return nil
		} else if (!a) {
			return b
		} else if (!b) {
			return a
		}

		var ret = [0, 0, 0, 0]
		Add.add_to(a, ret)
		if (b.type == VAR_TYPE_NUMBER or b.type == VAR_TYPE_INTEGER) {
			ret[0] = ret[0] / b.value
			ret[1] = ret[1] / b.value
			ret[2] = ret[2] / b.value
			ret[3] = ret[3] / b.value			
		} else if (b.type == VAR_TYPE_NUMBER2) {
			ret[0] = ret[0] / b.value.x
			ret[1] = ret[1] / b.value.y			
		} else if (b.type == VAR_TYPE_NUMBER3) {
			ret[0] = ret[0] / b.value.x
			ret[1] = ret[1] / b.value.y
			ret[2] = ret[2] / b.value.z
		} else if (b.type == VAR_TYPE_NUMBER4) {
			ret[0] = ret[0] / b.value.x
			ret[1] = ret[1] / b.value.y
			ret[2] = ret[2] / b.value.z
			ret[3] = ret[3] / b.value.w
		}

		if (a.type == VAR_TYPE_INTEGER and b.type == VAR_TYPE_INTEGER) {
			return Variant(VAR_TYPE_INTEGER, Math.floor(ret[0]))
		} else if (a.type == VAR_TYPE_NUMBER or a.type == VAR_TYPE_INTEGER) {
			return Variant(VAR_TYPE_NUMBER, ret[0])
		} else if (a.type == VAR_TYPE_NUMBER2) {
			return Variant(VAR_TYPE_NUMBER2, Vector2(ret[0], ret[1]))
		} else if (a.type == VAR_TYPE_NUMBER3) {
			return Variant(VAR_TYPE_NUMBER3, Vector3(ret[0], ret[1], ret[2]))
		} else if (a.type == VAR_TYPE_NUMBER4) {
			return Variant(VAR_TYPE_NUMBER4, Vector4(ret[0], ret[1], ret[2], ret[3]))
		} else {
			return nil
		}
	}
	
	calc_opcodes(compiler, opcodes, idx)
	{
		var r_a = Blueprint.calc_input_opcodes(this, 0, compiler, opcodes)
		var r_b = Blueprint.calc_input_opcodes(this, 1, compiler, opcodes)

		var r_dst = compiler.new_reg()
		if (r_dst) {
			opcodes.div(r_dst, r_a, r_b)
		}

		compiler.free_reg(r_a)
		compiler.free_reg(r_b)

		return r_dst
	}

	shader_gen(node, idx)
	{
		import "shadergraph.shader_builder" for ShaderBuilder

		var a = ShaderBuilder.gen_child(node, "a")
		var b = ShaderBuilder.gen_child(node, "b")

		if (!a and !b) {
			return nil
		} else if (!a) {
			return b
		} else if (!b) {
			return a
		} else {
			var bb = node.find_value("bb")
			return bb.div(a, b)
		}
	}
}