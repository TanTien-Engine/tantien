import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_NUMBER2
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "blueprint.node_editor" for NodeEditor
import "blueprint.blueprint" for Blueprint
import "blueprint.dirty_types" for DIRTY_EDITOR, DIRTY_OPCODES, DIRTY_COMPILE
import "maths.vector" for Vector2
import "maths.compiler" for Compiler as maths_Compiler
import "gui" for GUI
import "vm" for CodeTools

var EDITOR = nil

class Number2 is Node
{
	init() 
	{
		super.init()

		this.exports = [
			Pin(this, "v2", VAR_TYPE_NUMBER2),
		]

		this.layout()

		this.value = Vector2()

		this.pos_in_bytecode = []
	}

	on_pin_dirty(pin, type, msg)
	{
		super.on_pin_dirty(pin, type, msg)

		if (type == DIRTY_COMPILE) {
			this.pos_in_bytecode.clear()
		}
	}

	on_param_dirty()
	{
		if (this.pos_in_bytecode.isEmpty)
		{
			super.on_param_dirty()
		}
		else
		{
			var msg = []
			for (var pos in this.pos_in_bytecode) 
			{
				msg.add(pos + 2)
				msg.add(this.value.x)
				msg.add(pos + 6)
				msg.add(this.value.y)
			}

			Blueprint.send_pin_dirty_root(this.exports[0], DIRTY_OPCODES, msg)
		}
	}

	calc_value(idx) 
	{
		return Variant(VAR_TYPE_NUMBER2, this.value)
	}

	calc_opcodes(compiler, opcodes, idx)
	{
		this.pos_in_bytecode.add(CodeTools.get_size(opcodes))
		return maths_Compiler.create_vec2(compiler, opcodes, this.value)
	}

	layout() 
	{
		super.layout()

		this.style.height = this.style.height + DEFAULT_HEIGHT * 2
	}

	to_string(name) {
		return "
%(name).value.set(%(this.value.x), %(this.value.y))
"
	}

	draw_gui(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height * 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT

		var REGION = 1
		
		var vx = GUI.slider(ctx, "x", this.value.x, x, y, this.style.width - 20, REGION, false)
		if (vx != this.value.x) {
			this.value.x = vx
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var vy = GUI.slider(ctx, "y", this.value.y, x, y, this.style.width - 20, REGION, false)
		if (vy != this.value.y) {
			this.value.y = vy
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		return dirty
	}

	bind_editor()
	{
		if (!EDITOR) {
			EDITOR = NodeEditor("assets/blueprints/editorgraph/select_and_translate.ves")
		}

		Blueprint.connect(this.exports[0], EDITOR.editor_node.imports[1])

		Blueprint.send_node_dirty_root(EDITOR.editor_node)

		return true
	}

	draw_editor() 
	{
		if (EDITOR) {
			EDITOR.draw()		
		}
	}

	on_mouse_pressed(x, y, btn) 
	{
		EDITOR.on_mouse_pressed(x, y, btn)
	}

	on_mouse_released(x, y, btn) 
	{
		EDITOR.on_mouse_released(x, y, btn)
	}

	on_mouse_dragged(x, y, btn) 
	{
		EDITOR.on_mouse_dragged(x, y, btn)

		Blueprint.send_pin_dirty_root(this.exports[0], DIRTY_EDITOR)
	}
}