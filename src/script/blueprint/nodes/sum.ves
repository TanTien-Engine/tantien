import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY, VAR_TYPE_NUMBER_SET, VAR_TYPE_INTEGER, VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2, VAR_TYPE_NUMBER3, VAR_TYPE_NUMBER4
import "blueprint.blueprint" for Blueprint
import "blueprint.nodes.add" for Add
import "maths.vector" for Vector2, Vector3, Vector4
import "maths.compiler" for Compiler as maths_Compiler
import "vm" for CodeTools, CodeRegen

class Sum is Node
{
	init() 
	{
		super.init()

		this.imports = [
			Pin(this, "list", VAR_TYPE_ARRAY),
		]
		this.exports = [
			Pin(this, "val", VAR_TYPE_NUMBER_SET),
		]

		this.layout()
	}

	calc_value(idx)
	{
		var v_list = Blueprint.calc_input_value(this, 0)
		if (v_list.type != VAR_TYPE_ARRAY or v_list.value.isEmpty) {
			return nil
		}

		var sum = [0, 0, 0, 0]
		for (var item in v_list.value) {
			Add.add_to(item, sum)
		}

		var i = v_list.value[0]
		if (i.type == VAR_TYPE_NUMBER4) {
			return Variant(VAR_TYPE_NUMBER4, Vector4(sum[0], sum[1], sum[2], sum[3]))
		} else if (i.type == VAR_TYPE_NUMBER3) {
			return Variant(VAR_TYPE_NUMBER3, Vector3(sum[0], sum[1], sum[2]))
		} else if (i.type == VAR_TYPE_NUMBER2) {
			return Variant(VAR_TYPE_NUMBER2, Vector2(sum[0], sum[1]))
		} else {
			return Variant(VAR_TYPE_NUMBER, sum[0])
		}

		return nil
	}

	calc_opcodes(compiler, opcodes, idx)
	{
		var r_list = Blueprint.calc_input_opcodes(this, 0, compiler, opcodes)
		if (!r_list) {
			return nil
		}

		var r_dst = compiler.new_reg()

		var r_idx = maths_Compiler.create_number(compiler, opcodes, 0)

		var r_size = compiler.new_reg()
		opcodes.vector_size(r_size, r_list)

		var r_cmp = maths_Compiler.create_bool(compiler, opcodes, false)

		var begin_ip = CodeTools.get_size(opcodes)

		opcodes.cmp_num(r_cmp, r_idx, r_size)
		var offset_pos = CodeTools.get_size(opcodes) + 1
		var skip_ip = CodeTools.get_size(opcodes)
		opcodes.jump_if(0, r_cmp)

		var r_item = compiler.new_reg()
		opcodes.vector_fetch_r(r_item, r_list, r_idx)

		opcodes.add(r_dst, r_dst, r_item)

		opcodes.inc_num(r_idx)

		opcodes.jump(CodeTools.get_size(opcodes) - begin_ip)

		CodeRegen.write_int(opcodes, offset_pos, CodeTools.get_size(opcodes) - skip_ip)

		compiler.free_reg(r_list)
		compiler.free_reg(r_size)
		compiler.free_reg(r_idx)
		compiler.free_reg(r_cmp)
		compiler.free_reg(r_item)

		return r_dst
	}
}