import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_PORT, VAR_TYPE_ANY, VAR_TYPE_ARRAY
import "blueprint.blueprint" for Blueprint
import "blueprint.node_param" for NodeParam
import "blueprint.dirty_types" for DIRTY_EXECUTE

class ListAdd is Node
{
	init() 
	{
		super.init()

		this.imports = [
			Pin(this, "prev", VAR_TYPE_PORT),
			Pin(this, "list", VAR_TYPE_ARRAY),
			Pin(this, "add", [ VAR_TYPE_ANY, VAR_TYPE_ARRAY ]),
		]
		this.exports = [
			Pin(this, "next", VAR_TYPE_PORT),
		]

		this.params.add(NodeParam("unique", false))

		this.layout()
	}

	on_pin_dirty(pin, type)
	{
		if (pin == this.imports[1]) {
			Blueprint.send_pin_dirty(this.imports[2], type)
		}
	}

	execute_run() 
	{
		var v_list = Blueprint.calc_input_value(this, 1)
		var v_more = Blueprint.calc_input_value(this, 2)
		if (!v_list or !v_more) {
			return
		}

		var unique = this.query_param("unique").value

		var dirty = false
		if (v_more.value is List) {
			for (var item in v_more.value) {
				if (this.add(v_list.value, item, unique)) {
					dirty = true
				}
			}
		} else {
			if (this.add(v_list.value, v_more, unique)) {
				dirty = true
			}
		}

		if (dirty) {
			Blueprint.send_pin_dirty_root(this.imports[1], DIRTY_EXECUTE)
		}
	}

	add(list, item, unique)
	{
		var dirty = false

		if (unique)
		{
			for (var v in list) {
				if (v.value.equal(item.value)) {
					return dirty
				}
			}
			list.add(item)
			dirty = true
		}
		else
		{
			list.add(item)
			dirty = true
		}

		return dirty
	}
}