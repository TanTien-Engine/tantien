import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for VAR_TYPE_ANY
import "blueprint.blueprint" for Blueprint
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "blueprint.node_helper" for NodeHelper
import "gui" for GUI

class Input is Node
{
	init() 
	{
		super.init(Input.name)

		this.exports = [
			Pin(this, "var", VAR_TYPE_ANY),
		]

		this.layout()

		this.var_name = "in"
		this.var_type = VAR_TYPE_ANY

		this.parent_node = nil
		this.parent_pin_idx = -1
	}

	shader_gen(ctx)
	{
		if (this.parent_node) 
		{
			var prev = Blueprint.get_input_node(this.parent_node, this.parent_pin_idx)
			if (prev) {
				prev.shader_gen(ctx)			
			}
		}
	}	

	calc_value(idx)
	{
		return Blueprint.calc_input_value(this.parent_node, this.parent_pin_idx)
	}

	calc_inst(idx, ctx) 
	{
		return Blueprint.calc_input_inst(this.parent_node, this.parent_pin_idx, ctx)
	}

	to_string(name) 
	{
		if (this.var_name == "in")
		{
			var t_pin = Blueprint.get_first_connect_pin(this.exports[0])
			if (t_pin) {
				this.var_name = t_pin.name
				this.var_type = t_pin.type
			}
		}

		var type_str = NodeHelper.var_type_to_string(this.var_type)
		return "
%(name).var_name = \"%(this.var_name)\"
%(name).var_type = %(type_str)
"
	}

	get_title()
	{
		return "%(this.var_name)(I)"
	}
}