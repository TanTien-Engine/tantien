import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2, VAR_TYPE_NUMBER3
import "blueprint.node_param" for NodeParam
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "blueprint.blueprint" for Blueprint
import "rendergraph.variant" for VarTexture, VAR_TYPE_TEXTURE
import "image" for ImageData
import "graphics" for Graphics
import "render" for Texture2D, TextureSampler
import "gui" for GUI
import "math" for Math

class Texture is Node
{
	init() 
	{
		super.init()

		this.imports = [
			Pin(this, "tex",  VAR_TYPE_TEXTURE),
			Pin(this, "size", VAR_TYPE_NUMBER2),			
			Pin(this, "col",  VAR_TYPE_NUMBER3),			
		]
		this.exports = [
			Pin(this, "tex", VAR_TYPE_TEXTURE),
			Pin(this, "width", VAR_TYPE_NUMBER),
			Pin(this, "height", VAR_TYPE_NUMBER),
		]

		this.layout()

		this.tex = nil
		this.sampler = nil

		this.tex_filepath = nil

		this.width  = 0
		this.height = 0
		this.format = "rgba8"

		this.gamma_correction = false

		this.params.add(NodeParam("unif_name", "u_tex"))

		// for gui
		this.sampler_items = ["null", "nearest_clamp", "linear_clamp", "linear_clamp_mipmap", "nearest_repeat", "linear_repeat"]
		this.sampler_selected = 0
		this.fomat_items = ["rgb", "rgba8", "rgba16f", "rg16f", "r16"]
		this.format_selected = 0
	}

	calc_value(idx)
	{
		if (idx == 0)
		{
			var ret = VarTexture()
			ret.tex = this.get_texture()
			ret.sampler = this.get_sampler()
			return Variant(VAR_TYPE_TEXTURE, ret)
		}
		else if (idx == 1)
		{
			var width = 0
			var tex = this.get_texture()
			if (tex) {
				width = tex.get_width()		
			}
			return Variant(VAR_TYPE_NUMBER, width)
		}
		else if (idx == 2)
		{
			var height = 0
			var tex = this.get_texture()
			if (tex) {
				height = tex.get_height()
			}
			return Variant(VAR_TYPE_NUMBER, height)
		}

		return nil
	}

	render_gen()
	{
		var v_col = Blueprint.calc_input_value(this, 2)
		if (v_col) 
		{
			this.width  = 1
			this.height = 1
			this.format = "rgba8"
			this.tex = this.create_texture()

			var rgb = v_col.value
			var r = Math.min(Math.max(rgb.x, 0.0), 1.0) * 255.0
			var g = Math.min(Math.max(rgb.y, 0.0), 1.0) * 255.0
			var b = Math.min(Math.max(rgb.z, 0.0), 1.0) * 255.0
			var col = r + g * 256 + b * 256 * 256 + 255 * 256 * 256 * 256
			this.tex.upload([col], 0, 0, 1, 1)
		}
	}

	shader_gen(node, idx)
	{
		var module = node.find_value("module")

		var unif_pair = node.tree.ud["global"].uniforms.query(this)
		if (!unif_pair) 
		{
			var global = node.tree.ud["global"]

			var name = this.query_param("unif_name").value
			var unif = global.gen.add_uniform(module, name, "texture")
			var unif_name = global.gen.query_unif_name(unif)

			unif_pair = [unif, unif_name]
			node.tree.ud["global"].uniforms.add(this, unif_pair)
		}

		if (idx == 0)
		{
			var tex = this.get_texture()
			if (tex and unif_pair[0]) {
				var bb = node.find_value("bb")
				return bb.load(unif_pair[0])
			} else {
				return nil
			}
		}
		else if (idx == 1)
		{
			var width = 0
			var tex = this.get_texture()
			if (tex) {
				width = tex.get_width()
			}
			return module.const_float(width)
		}
		else if (idx == 2)
		{
			var height = 0
			var tex = this.get_texture()
			if (tex) {
				height = tex.get_height()
			}
			return module.const_float(height)
		}
		else
		{
			return nil
		}
	}

	bind_uniforms(prog, uniforms)
	{
		var unif_pair = uniforms.query(this)
		if (unif_pair and unif_pair[1]) {
			var tex     = this.get_texture()
			var sampler = this.get_sampler()
			prog.set_uniform_value([unif_pair[1], "sampler", [tex, sampler]])
		}
	}

	draw(mt, cam_mt)
	{
		super.draw(mt, cam_mt)

		var tex = this.get_texture()
		if (tex)
		{
			var scale = Math.min(1.0, this.style.width / tex.get_width())
			var x = this.pos.x
			var y = this.pos.y + this.style.height * 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT - tex.get_height() * scale * 0.5 - DEFAULT_HEIGHT
			Graphics.draw_texture(tex, [x, y], 0, [scale, scale], [0, 0])
		}
	}

	layout()
	{
		super.layout()

		// sampler
		this.style.width = 200
		this.style.height = this.style.height + DEFAULT_HEIGHT

		// gamma_correction
		this.style.height = this.style.height + DEFAULT_HEIGHT
	}

	to_string(name) 
	{
		var ret = super.to_string(name)
		ret = ret + "%(name).gamma_correction = %(this.gamma_correction)\n"
		if (this.tex_filepath) {
			ret = ret + "%(name).init_texture(\"%(this.tex_filepath)\")\n"		
		} else {
			ret = ret + "%(name).init_texture(%(this.width), %(this.height), \"%(this.format)\")\n"
		}
		if (this.sampler) {
			ret = ret + "%(name).set_sampler(\"%(this.sampler_items[this.sampler_selected])\")\n"
		}
		return ret
	}

	draw_gui(ctx)
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height* 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT

		var item = GUI.combo(ctx, "sampler", this.sampler_selected, this.sampler_items, x, y, 130)
		if (item != this.sampler_selected) {
			this.sampler_selected = item
			this.sampler = TextureSampler.init(this.sampler_items[this.sampler_selected])
			dirty = true
		}

		y = y - DEFAULT_HEIGHT
		var _gamma_correction = GUI.checkbox(ctx, "gamma_correction", this.gamma_correction, x, y)
		if (_gamma_correction != this.gamma_correction) 
		{
			this.gamma_correction = _gamma_correction
			dirty = true
			this.init_texture(this.tex_filepath)
		}

		return dirty
	}

	init_texture(filepath)
	{
		if (!filepath or filepath == "") {
			return
		}

		this.tex_filepath = filepath

		var img = ImageData.init(filepath)
		this.tex = Texture2D.init(img, this.gamma_correction)
	}

	init_texture(width, height, format)
	{
		this.width  = width
		this.height = height
		this.format = format
	}

	set_sampler(name)
	{
		for (var i in 0..this.sampler_items.count) {
			if (this.sampler_items[i] == name) {
				this.sampler_selected = i
				this.sampler = TextureSampler.init(name)
				return
			}
		}
	}

	init_unif_name(name)
	{
		if (this.query_param("unif_name").value == "u_tex") {
			this.query_param("unif_name").value = name
		}
	}

	create_texture()
	{
		var w = this.width
		var h = this.height

		if (w == 0 or h == 0) 
		{
			var v_size = Blueprint.calc_input_value(this, 1)
			if (v_size) {
				w = v_size.value.x
				h = v_size.value.y
			} else {
				w  = 32
				h = 32
			}
		}

		return Texture2D.init(w, h, this.format)
	}

	get_texture()
	{
		var v_tex = Blueprint.calc_input_value(this, 0)
		if (v_tex and v_tex.value.tex) {
			return v_tex.value.tex
		} else {
			if (!this.tex) {
				this.tex = this.create_texture()
			}
			return this.tex
		}
	}

	get_sampler()
	{
		if (this.sampler) {
			return this.sampler
		}

		var v_tex = Blueprint.calc_input_value(this, 0)
		if (v_tex) {
			return v_tex.value.sampler
		}

		return nil
	}	
}