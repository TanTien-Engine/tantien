import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_STRING
import "blueprint.node_helper" for NodeHelper
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "blueprint.blueprint" for Blueprint
import "rendergraph.variant" for VAR_TYPE_SHADER
import "rendergraph.variates.shader" for Shader as var_Shader
import "rendergraph.components.shader_uniforms" for ShaderUniforms
import "rendergraph.nodes.shader_code" for ShaderCode
import "gui" for GUI
import "render" for Shader as tt_Shader
import "filesystem" for Filesystem

class Shader is Node
{
	init() 
	{
		super.init(Shader.name)

		this.imports = [
			Pin(this, "cs", VAR_TYPE_STRING)
		]
		this.exports = [
			Pin(this, "out", VAR_TYPE_SHADER)
		]

		this.vs  = ""
		this.tcs = ""
		this.tes = ""		
		this.gs  = ""
		this.fs  = ""
		this.cs  = ""

		this.shader = var_Shader(this)
		this.need_rebuild = false

		this.uniforms = []

		this.layout()
	}

	calc_value(idx)
	{
		return Variant(VAR_TYPE_SHADER, this.shader)
	}

	get_shader_uniforms(cs, inc_dir)
	{
		var uniforms = []
		ShaderUniforms.get_shader_uniforms("vertex",    this.vs,  nil, "glsl", uniforms, "main")
		ShaderUniforms.get_shader_uniforms("tess_ctrl", this.tcs, nil, "glsl", uniforms, "main")
		ShaderUniforms.get_shader_uniforms("tess_eval", this.tes, nil, "glsl", uniforms, "main")
		ShaderUniforms.get_shader_uniforms("geometry",  this.gs,  nil, "glsl", uniforms, "main")
		ShaderUniforms.get_shader_uniforms("pixel",     this.fs,  nil, "glsl", uniforms, "main")
		if (cs) {
			ShaderUniforms.get_shader_uniforms("compute", cs, inc_dir, "glsl", uniforms, "main")
		} else {
			ShaderUniforms.get_shader_uniforms("compute", this.cs, nil, "glsl", uniforms, "main")		
		}
		return uniforms
	}

	render_gen()
	{
		if (!this.need_rebuild and this.shader.prog) {
			return
		}

		var inc_dir = nil

		var cs = nil
		var v_cs = Blueprint.calc_input_value(this, 0)
		if (v_cs and v_cs.type == VAR_TYPE_STRING) 
		{
			cs = v_cs.value

			// fixme
			var n_cs = Blueprint.get_input_node(this, 0)
			if (n_cs and n_cs is ShaderCode) {
				inc_dir = Filesystem.get_directory(n_cs.filepath)
			}
		}

		this.shader.prog = nil
		if (cs) {
			this.shader.prog = tt_Shader.init(cs, inc_dir)
		} else if (this.cs != "") {
			this.shader.prog = tt_Shader.init(this.cs, nil)
		} else if (this.vs != "" and this.fs != "") {
			this.shader.prog = tt_Shader.init(this.vs, this.tcs, this.tes, this.gs, this.fs, nil)
		}

		if (this.shader.prog)
		{
			this.uniforms = this.get_shader_uniforms(cs, inc_dir)
			for (var unif in this.uniforms) {
				ShaderUniforms.add_uniform_to_imports(this, unif)
			}

			this.layout()
		}
	}

	layout() 
	{
		super.layout()

		this.style.width = 180

		for (var unif in this.uniforms) {
			ShaderUniforms.layout(this, unif)
		}
	}

	on_connected(pin)
	{
		if (pin.is_input and pin.slot_idx == 0) {
			this.render_gen()
		}
	}

	set_uniform(unif, name, value)
	{
		if (unif[1] == "array" or unif[1] == "struct")
		{
			for (var u in unif[2]) {
				this.set_uniform(u, name, value)
			}		
		}
		else if (unif[0] == name)
		{
			for (var i in 0..value.count) {
				unif[2][i] = value[i]
			}
		}
	}

	set_uniform(name, value)
	{
		for (var unif in this.uniforms) {
			this.set_uniform(unif, name, value)
		}
	}

	uniform_to_string(name, unif)
	{
		var ret = ""

		for (var p in this.imports) {
			if (p.name == unif[0]) {
				if (!p.conns.isEmpty) {
					return ret
				}
			}
		}

		if (unif[1] == "array" or unif[1] == "struct") 
		{
			for (var u in unif[2]) {
				ret = ret + this.uniform_to_string(name, u)
			}
		} 
		else 
		{
			if (unif[1] == "float" or unif[1] == "int" or unif[1] == "bool") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]) ])\n"
			} else if (unif[1] == "float2" or unif[1] == "int2") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]), %(unif[2][1]) ])\n"
			} else if (unif[1] == "float3" or unif[1] == "int3") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]), %(unif[2][1]), %(unif[2][2]) ])\n"
			} else if (unif[1] == "float4" or unif[1] == "int4") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]), %(unif[2][1]), %(unif[2][2]), %(unif[2][3]) ])\n"
			}
		}

		return ret
	}

	to_string(name) 
	{
		var code = "
%(name).vs = \"%(this.vs)\"
%(name).tcs = \"%(this.tcs)\"
%(name).tes = \"%(this.tes)\"
%(name).gs = \"%(this.gs)\"
%(name).fs = \"%(this.fs)\"
%(name).cs = \"%(this.cs)\"
%(name).render_gen()
"
		for (var unif in this.uniforms) {
			code = code + this.uniform_to_string(name, unif)
		}
		return code
	}

	draw_gui(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height* 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT

		for (var unif in this.uniforms) {
			var y_d = ShaderUniforms.draw_gui(this, ctx, unif, x, y)
			y = y_d[0]
			if (y_d[1]) {
				dirty = true
			}
		}

		return dirty
	}
}