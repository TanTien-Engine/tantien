import "render" for Render
import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.Blueprint" for Blueprint
import "blueprint.variant" for VAR_TYPE_PORT, VAR_TYPE_ARRAY
import "gui" for GUI
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "rendergraph.variant" for VAR_TYPE_SHADER, VAR_TYPE_VERTEX_ARRAY, VAR_TYPE_FRAME_BUFFER, VAR_TYPE_MODEL, VAR_TYPE_RENDER_STATE
import "math" for Math
import "render" for RenderState

class Draw is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "prev",      VAR_TYPE_PORT),
			Pin(this, "shader",    VAR_TYPE_SHADER),
			Pin(this, "va",        VAR_TYPE_VERTEX_ARRAY),
			Pin(this, "model",     VAR_TYPE_MODEL),
			Pin(this, "inst_mats", VAR_TYPE_ARRAY),
			Pin(this, "rs",        VAR_TYPE_RENDER_STATE),				
		]
		this.exports = [
			Pin(this, "next", VAR_TYPE_PORT)
		]

		this.prim_types = [ "points", "lines", "line_strip", "triangles", "tri_strip", "tri_adjacency", "patches" ]
		this.prim_selected = 1

		this.render_state = { 
			"depth_test" : false, 
			"depth_func" : "less", 
			"cull" : "disable",
			"rasterization" : "fill",
			"clip_plane" : false,
			"blend" : false,
			"prim_restart" : false,
		}

		// for gui
		this.depth_func_items = ["never", "less", "equal", "lequal", "greater", "notequal", "gequal", "always"]
		this.depth_func_selected = -1
		this.cull_items = ["disable", "front", "back", "front_and_back"]
		this.cull_selected = -1
		this.rasterization_items = ["point", "line", "fill"]
		this.rasterization_selected = 2

		this.skip = false

		this.layout()
	}

	render_run() 
	{
		if (this.skip) {
			return
		}

		var v_shader = Blueprint.calc_input_value(this, 1).value
		if (!v_shader or !v_shader.prog) {
			return
		}

		var model = Blueprint.calc_input_value(this, 3).value
		if (model and model.model)
		{
			this.draw_model(v_shader, model.model)
			return
		}

		var va = nil
		var v_va = Blueprint.calc_input_value(this, 2)
		if (v_va) {
			va = v_va.value
		}
		if (!va) {
			return
		}

		var count = v_shader.get_frame_count()
		if (va is List) {
			count = Math.max(count, va.count)
			if (va.isEmpty)	{
				return
			}	
		}

		var render_state = this.build_render_state()

		var mats = this.get_input_inst_mats()
		for (var i in 0..count) 
		{
			v_shader.update_uniforms(i)

			var _va = va
			if (va is List) 
			{
				if (i < va.count) {
					_va = va[i].value
				} else {
					_va = va[va.count - 1].value
				}
			}

			if (mats) 
			{
				Render.draw_instanced(this.prim_types[this.prim_selected], v_shader.prog, _va, render_state, mats)
			} 
			else 
			{
				Render.draw(this.prim_types[this.prim_selected], v_shader.prog, _va, render_state)
			}
		}
	}

	draw_model(shader, model)
	{
		var mats = this.get_input_inst_mats()

		var render_state = this.build_render_state()

		var count = shader.get_frame_count()
		for (var i in 0..count) 
		{
			shader.update_uniforms(i)

			if (mats) 
			{
				Render.draw_model_instanced(shader.prog, model, render_state, mats)
			} 
			else 
			{
				Render.draw_model(shader.prog, model, render_state)			
			}
		}
	}

	build_render_state()
	{
		var rs = nil
		var v_rs = Blueprint.calc_input_value(this, 5)
		if (v_rs) {
			rs = v_rs.value
		}
		if (!rs) {
			rs = RenderState.init()
		}

		for (var entry in this.render_state)
		{
			var key = entry.key
			var val = entry.value

			if (key == "depth_test") {
				rs.z_test(val, this.render_state["depth_func"])
			} else if (key == "cull") {
				rs.face_culling(val)
			} else if (key == "rasterization") {
				rs.rasterization_mode(val)
			} else if (key == "clip_plane") {
				rs.clip_plane(val)
			} else if (key == "blend") {
				rs.blending(val)
			} else if (key == "prim_restart") {
				rs.prim_restart(val)
			} 
		}

		return rs
	}

	layout() 
	{
		super.layout()

		this.style.height = this.style.height + DEFAULT_HEIGHT * 7
	}

	to_string(name) 
	{
		return "
%(name).set_prim_type(\"%(this.prim_types[this.prim_selected])\")
%(name).render_state = %(this.render_state)
%(name).skip = %(this.skip)
"
	}

	draw_gui(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height* 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT

		var skip = GUI.checkbox(ctx, "skip", this.skip, x, y)
		if (skip != this.skip) {
			this.skip = skip
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var item = GUI.combo(ctx, "prim", this.prim_selected, this.prim_types, x, y, 80)
		if (item != this.prim_selected) {
			this.prim_selected = item
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var depth_test = this.render_state["depth_test"]
		var _depth_test = GUI.checkbox(ctx, "depth test", depth_test, x, y)
		if (_depth_test != depth_test) {
			this.render_state["depth_test"] = _depth_test
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		if (this.depth_func_selected < 0) {
			var depth_func = this.render_state["depth_func"]
			this.depth_func_selected = this.depth_func_items.query_index(depth_func)
		}
		item = GUI.combo(ctx, "depth_func", this.depth_func_selected, this.depth_func_items, x, y, 80)
		if (item != this.depth_func_selected) {
			this.depth_func_selected = item
			this.render_state["depth_func"] = this.depth_func_items[this.depth_func_selected]
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		if (this.cull_selected < 0) {
			var cull = this.render_state["cull"]
			this.cull_selected = this.cull_items.query_index(cull)
		}

		item = GUI.combo(ctx, "cull", this.cull_selected, this.cull_items, x, y, 80)
		if (item != this.cull_selected) {
			this.cull_selected = item
			this.render_state["cull"] = this.cull_items[this.cull_selected]
			dirty = true				
		}
		y = y - DEFAULT_HEIGHT

		item = GUI.combo(ctx, "rasterization", this.rasterization_selected, this.rasterization_items, x, y, 80)
		if (item != this.rasterization_selected) {
			this.rasterization_selected = item
			this.render_state["rasterization"] = this.rasterization_items[this.rasterization_selected]
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		if (this.render_state["clip_plane"] == nil) {
			this.render_state["clip_plane"] = false
		}
		var clip_plane = GUI.checkbox(ctx, "clip_plane", this.render_state["clip_plane"], x, y)
		if (clip_plane != this.render_state["clip_plane"]) {
			this.render_state["clip_plane"] = clip_plane
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		if (this.render_state["blend"] == nil) {
			this.render_state["blend"] = false
		}
		var blend = GUI.checkbox(ctx, "blend", this.render_state["blend"], x, y)
		if (blend != this.render_state["blend"]) {
			this.render_state["blend"] = blend
			dirty = true
		}
		y = y - DEFAULT_HEIGHT
		
		return dirty
	}

	set_prim_type(prim)
	{
		for (var i in 0..this.prim_types.count) {
			if (this.prim_types[i] == prim) {
				this.prim_selected = i
				// todo break
			}		
		}
	}

	get_input_inst_mats()
	{
		var v_inst_mats = Blueprint.calc_input_value(this, 4)
		if (!v_inst_mats) {
			return nil
		}

		var mats = []

		if (v_inst_mats.value is List) {
			for (var i in v_inst_mats.value) {
				mats.add(i.value)
			}
		} else {
			mats.add(v_inst_mats.value)
		}

		return mats
	}
}